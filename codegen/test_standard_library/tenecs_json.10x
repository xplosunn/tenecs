package test

import tenecs.error.Error
import tenecs.json.JsonField
import tenecs.json.jsonList
import tenecs.json.jsonBoolean
import tenecs.json.jsonInt
import tenecs.json.jsonObject0
import tenecs.json.jsonObject1
import tenecs.json.jsonObject2
import tenecs.json.jsonOr
import tenecs.json.jsonString
import tenecs.string.join
import tenecs.test.UnitTestKit
import tenecs.test.UnitTestRegistry
import tenecs.test.UnitTestSuite

struct Post(
  title: String
)

struct Task(title: String, done: Boolean)

_ := UnitTestSuite(
  "jsonStringTests",
  (registry: UnitTestRegistry): Void => {
    fromJson := jsonString().fromJson
    toJson := jsonString().toJson
    registry.test("empty", (testkit: UnitTestKit): Void => {
      testkit.assert.equal("", fromJson("\"\""))
      testkit.assert.equal(toJson(""), "\"\"")
    })
    registry.test("foo", (testkit: UnitTestKit): Void => {
      testkit.assert.equal("foo", fromJson("\"foo\""))
      testkit.assert.equal(toJson("foo"), "\"foo\"")
    })
    registry.test("error", (testkit: UnitTestKit): Void => {
      testkit.assert.equal(Error("Could not parse String from 1"), fromJson("1"))
    })
  }
)

_ := UnitTestSuite(
  "jsonBooleanTests",
  (registry: UnitTestRegistry): Void => {
    fromJson := jsonBoolean().fromJson
    toJson := jsonBoolean().toJson
    registry.test("true", (testkit: UnitTestKit): Void => {
      testkit.assert.equal(true, fromJson("true"))
      testkit.assert.equal(toJson(true), "true")
    })
    registry.test("false", (testkit: UnitTestKit): Void => {
      testkit.assert.equal(false, fromJson("false"))
      testkit.assert.equal(toJson(false), "false")
    })
    registry.test("fail f", (testkit: UnitTestKit): Void => {
      testkit.assert.equal(Error("Could not parse Boolean from f"), fromJson("f"))
    })
  }
)

_ := UnitTestSuite(
  "jsonIntTests",
  (registry: UnitTestRegistry): Void => {
    fromJson := jsonInt().fromJson
    toJson := jsonInt().toJson
    registry.test("0", (testkit: UnitTestKit): Void => {
      testkit.assert.equal(0, fromJson("0"))
      testkit.assert.equal(toJson(0), "0")
    })
    registry.test("1234567", (testkit: UnitTestKit): Void => {
      testkit.assert.equal(1234567, fromJson("1234567"))
      testkit.assert.equal(toJson(1234567), "1234567")
    })
    registry.test("fail 0.1", (testkit: UnitTestKit): Void => {
      testkit.assert.equal(Error("Could not parse Int from 0.1"), fromJson("0.1"))
    })
  }
)

_ := UnitTestSuite(
  "jsonListTests",
  (registry: UnitTestRegistry): Void => {
    registry.test("empty string list", (testkit: UnitTestKit): Void => {
      Converter := jsonList(jsonString())
      testkit.assert.equal(<String>[], Converter.fromJson("[]"))
      testkit.assert.equal(Converter.toJson(<String>[]), "[]")
    })
    registry.test("list with empty list", (testkit: UnitTestKit): Void => {
      Converter := jsonList(jsonList(jsonString()))
      testkit.assert.equal(<List<String>>[<String>[]], Converter.fromJson("[[]]"))
      testkit.assert.equal(Converter.toJson(<List<String>>[<String>[]]), "[[]]")
    })
    registry.test("nested failure", (testkit: UnitTestKit): Void => {
      Converter := jsonList(jsonString())
      testkit.assert.equal(Error("Could not parse String from 1"), Converter.fromJson("[1]"))
    })
    registry.test("couple elements", (testkit: UnitTestKit): Void => {
      Converter := jsonList(jsonString())
      testkit.assert.equal(<String>["a", "b"], Converter.fromJson("[\"a\", \"b\"]"))
      testkit.assert.equal(Converter.toJson(<String>["a", "b"]), "[\"a\",\"b\"]")
    })
  }
)

_ := UnitTestSuite(
  "jsonObjectTests",
  (registry: UnitTestRegistry): Void => {
    registry.test("jsonObject0", (testkit: UnitTestKit): Void => {
      assert := testkit.assert
      Converter := jsonObject0((): String => {
        "ok"
      })
      assert.equal("ok", Converter.fromJson("{}"))
      assert.equal("ok", Converter.fromJson("{\"a\":true}"))
      assert.equal(Error("Could not parse object from 1"), Converter.fromJson("1"))
      assert.equal(Converter.toJson(""), "{}")
      assert.equal(Converter.toJson("foo"), "{}")
    })
    registry.test("jsonObject1", (testkit: UnitTestKit): Void => {
      assert := testkit.assert
      Converter := jsonObject1(Post, JsonField("title", jsonString(), (post: Post) => post.title))
      assert.equal(Post("the title"), Converter.fromJson("{\"title\":\"the title\"}"))
      assert.equal(Converter.toJson(Post("the title")), "{\"title\":\"the title\"}")
      assert.equal(Error("Could not find object field \"title\" in {\"a\":true}"), Converter.fromJson("{\"a\":true}"))
      assert.equal(Error("Could not parse object field \"title\": Could not parse String from true"), Converter.fromJson("{\"title\":true}"))
      assert.equal(Error("Could not parse object from 1"), Converter.fromJson("1"))
    })
    registry.test("jsonObject2", (testkit: UnitTestKit): Void => {
      assert := testkit.assert
      Converter := jsonObject2(
        Task,
        JsonField("title", jsonString(), (task: Task) => task.title),
        JsonField("done", jsonBoolean(), (task: Task) => task.done)
      )
      assert.equal(Task("do it", false), Converter.fromJson("{\"title\":\"do it\",\"done\":false}"))
      assert.equal(Converter.toJson(Task("do it", false)), "{\"done\":false,\"title\":\"do it\"}")
      assert.equal(Task("done it", true), Converter.fromJson("{\"title\":\"done it\",\"done\":true}"))
      assert.equal(Converter.toJson(Task("done it", true)), "{\"done\":true,\"title\":\"done it\"}")
      assert.equal(Error("Could not find object field \"title\" in {\"done\":true}"), Converter.fromJson("{\"done\":true}"))
      assert.equal(Error("Could not find object field \"done\" in {\"title\":\"do\"}"), Converter.fromJson("{\"title\":\"do\"}"))
      assert.equal(Error("Could not parse object field \"title\": Could not parse String from true"), Converter.fromJson("{\"title\":true}"))
      assert.equal(Error("Could not parse object field \"done\": Could not parse Boolean from 1"), Converter.fromJson("{\"title\":\"do it\",\"done\":1}"))
      assert.equal(Error("Could not parse object from 1"), Converter.fromJson("1"))
    })
  }
)

_ := UnitTestSuite(
  "parseOrTests",
  (registry: UnitTestRegistry): Void => {
    Converter := jsonOr(jsonString(), jsonBoolean(), (strOrBool) => when strOrBool {
      is String => { jsonString() }
      is Boolean => { jsonBoolean() }
    })
    registry.test("string", (testkit: UnitTestKit): Void => {
      testkit.assert.equal("foo", Converter.fromJson("\"foo\""))
      testkit.assert.equal(Converter.toJson("foo"), "\"foo\"")
    })
    registry.test("boolean", (testkit: UnitTestKit): Void => {
      testkit.assert.equal(true, Converter.fromJson("true"))
      testkit.assert.equal(Converter.toJson(true), "true")
    })
    registry.test("error", (testkit: UnitTestKit): Void => {
      testkit.assert.equal(Error("Could not parse from 1"), Converter.fromJson("1"))
    })
  }
)


